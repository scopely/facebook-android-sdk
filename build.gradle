/**
 * Usage:
 * Set artifactoryDeployUser and artifactoryDeployPassword in ~/.gradle/gradle.properties
 *
 * ./gradlew uploadArchives
 *
 * Done!
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.6+'
    }
}

apply plugin: 'android-library'
apply plugin: 'maven'

android {
    compileSdkVersion 17
    buildToolsVersion "18.1.1"

    sourceSets {
        main {
            manifest {
                // there's only ever one file so srcFile replaces it.
                srcFile 'facebook/AndroidManifest.xml'
            }
            java {
                srcDirs = ['facebook/src']
            }
            res {
                srcDirs = ['facebook/res']
            }
            assets {
                srcDirs = ['facebook/assets']
            }
            resources {
                srcDirs = ['facebook/src']
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.support:support-v4:13.0.+'
}

version = '3.5.2-SNAPSHOT'
group = 'com.facebook'

uploadArchives {
    repositories {
        // the maven plugin features this deployer
        mavenDeployer {
            // we deploy to the release repository in this case
            repository(url: "http://scopelybin.artifactoryonline.com/scopelybin/libs-snapshots-local/") {
                authentication(userName: artifactoryDeployUser, password: artifactoryDeployPassword)
            }
            pom.project {
                artifactId 'facebook-android-sdk'
                name 'Facebook Android SDK'
                packaging 'aar'
            }
        }
    }
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.8'
}
